//a-p demo code//

GRAPHIC

DISG lab 35 19
loadsound tard3
tardisf=1
DISS 11 581 144
ACTIVATE 11
loadsound pour
slave pour 2 4 64
pourf=1
ty:
ty:
ty:
ty:
ty:
ty:  Welcome, adventurer!  For this demo I will give you this time
ty:machine, the tardis, for your adventuring.  Type "enter tardis"
ty:to move between rooms.  Good Luck!
ty:

MAIN

v:mouseclick
  if mousex>562
  and
  if mousex<604
  and
  if mousey>144
  and
  if mousey<182
    inject ENTER TARDIS
    endif
  if mousex>514
  and
  if mousex<591
  and
  if mousey>96
  and
  if mousey<122
    DISS 1 0 0
    ACTIVATE 1
    ty:
    ty:This adventure demo was created using the Adventure-Pro
    ty:Adventure Language.  Adventure-Pro is an honest-to-goodness
    ty:language dedicated to adventure construction on the Amiga
    ty:computer.  It contains powerful graphics and sound commands,
    ty:as well as a very flexible location-based adventure database.
    ty:Although it can be used to create standard adventures, it
    ty:has the powerful math and disk-IO routines necessary to
    ty:create entire adventure systems!
    ty:Adventure-Pro was written at SunRize Industries by Anthony
    ty:Wood and Thom Robertson. The graphics for this demo were
    ty:produced by the steady hand of John Bruner.
    ty:
    ty:    Watch for complete adventures coming soon from
    ty:                    SunRize Ind.
    ty:
    ENDS 1
    done
  endif
  if mousex>428
    if mousex<466
      if mousey>83
      and
      if mousey<99
        inject NORTH
      endif
      if mousey>118
      and
      if mousey<137
        inject SOUTH
      endif
    else
      if mousey<118
      and
      if mousey>99
      and
      if mousex<496
        inject EAST
      endif
    endif
  else
    if mousey<118
    and
    if mousey>99
    and
    if mousex>386
      inject WEST
    endif
  endif
  ndone

v:use
  n:disk
    ty:The disk is worthless unless it is put into
    ty:a disk drive.
    done

v:use,fire,shoot
  n:gun,raygun
    if IN$ gun
      ty:There's no charge left!
    else
      ty:You don't have any gun!
    done

v:shoot
  if IN$ gun
    ty:There's no charge left!
  else
    ty:You don't have any gun!
  done

v:go,enter,use,activate
  n:tardis
    playsound tard3 1 64
    if room=1
      toroom 2
    else
    if room=2
      toroom 3
    else
    if room=3
      toroom 1
    endif
    endif
    endif
    done
  n:door,gate,doors
    if room=2
      if dooro=1
        ty:You don't want to go in there!!
        ty:The eyes! Don't you see the eyes!?!
        done
      else
        ty:the door isn't open.
        done
      endif
    else
      ty:there's no door here!
      done
    endif  
  nend
  ty:Enter the what!?!
  done


v:open
  n:door,doors,gates,gate
    if room=2
    and
    if dooro=0
      loadsound creak.1
      DISS 5 0 0
      ACTIVATE 5
      playsound creak.1 1 64
      dooro=1
      wait 5
      ENDS 5
      changermg 2 dungeon2
      changerms 6 2
      clearsound creak.1
      done
    else
      if dooro=1
        ty:it's already open!
      else
        ty:No door here!
      endif
    done

v:close,shut
  n:door,doors,gates,gate
    if room=2
    and
    if dooro=1
      changerms 6 0
      loadsound creak.2
      DISS 8 0 0
      ACTIVATE 8
      playsound creak.2 1 64
      dooro=0
      wait 8
      ENDS 8
      changermg 2 dungeon
      clearsound creak.2
      done
    else
      if dooro=0
        ty:it's already shut!
      else
        ty:No door here!
      endif
    done

v:shit,fuck,ass,asshole,motherfucker,mutherfucker,piss,hell
  ty:tsk, tsk!  Such language!
  done

v:break,destroy,get,take,speak,talk,grab,shake,touch,push,examine
  n:skeleton,bones
    if room=2
      ty:Sam the Skeleton says:
      ty:Don't bother me. I'm just hanging around.
    else
      ty:There's no skeleton here!
    endif
    done

  n:alien
    if room=3
      ty:Albert the Alien says:
      ty:Watch it, Bub!
    else
      ty:There's no alien here!
    endif
    done

  n:scientist,man
    if room=1
      ty:Scott the Scientist says:
      ty:Here in the SunRize labs we
      ty:never run out of ideas (or
      ty:this fluid)!
    else
      ty:There's no scientist here!
    endif
    done

  p:scientist,man
    if room=1
      ty:Scott the Scientist says:
      ty:Here in the SunRize labs we
      ty:never run out of ideas (or
      ty:this fluid)!
    else
      ty:There's no scientist here!
    endif
    done

  p:skeleton,bones
    if room=2
      ty:Sam the Skeleton says:
      ty:Don't bother me. I'm just hanging around.
    else
      ty:There's no skeleton here!
    endif
    done

  p:alien
    if room=3
      ty:Albert the Alien says:
      ty:Watch it, Bub!
    else
      ty:There's no alien here!
    endif
    done

  n:treasure,chest,gold,sword
    if room=2
      ty:Uh... it's all fake! Yeah! Uh... and it's glued down,
      ty:too! Yeah! That's the ticket!
      done
    else
      ty:You see no treasure!
      done
    endif

  n:torch
    if room=2
      ty:It's bolted to the wall. No amount of prying will
      ty:get it off.
      done
    else
      ty:You see no torch here!
      done
    endif

  n:computer,amiga
    if room=1
      ty:It's bolted to the table. No amount of prying will
      ty:get it off.
      done
    else
      ty:You see no Amiga here!
      done
    endif


v:get,pick,grab,take,carry
  n:tardis
    ty:You can't get that, only ride in it!
    done
  n:crown,headband,tiara,jewelry
    if RM$ tiara
      ty:ok!
      rm- tiara
      in+ tiara
      done
    else
      ty:That's not here!
      done
    endif
  n:gun,raygun
    if RM$ gun
      ty:ok!
      rm- gun
      in+ gun
      done
    else
      ty:That's not here!
      done
    endif
  n:disk
    if RM$ disk
      ty:ok!
      rm- disk
      in+ disk
      done
    else
      ty:That's not here!
      done
    endif
  nend
  ty:That's not here!
  done
  endif
    
v:drop,lay,set,leave,place
  n:tardis
    ty:You can't even get that, much less drop it!
    done
  n:crown,headband,tiara,jewelry
    if IN$ tiara
      ty:ok!
      in- tiara
      rm+ tiara
      done
    else
      ty:You don't have that!
      done
    endif
  n:gun,raygun
    if IN$ gun
      ty:ok!
      in- gun
      rm+ gun
      done
    else
      ty:You don't have that!
      done
    endif
  n:disk
    if IN$ disk
      ty:ok!
      in- disk
      rm+ disk
      done
    else
      ty:You don't have that!
      done
    endif
  nend
  ty:You don't have that!
  done
  endif

v:look
  sdone

v:examine,inspect
  n:tardis
    ty:It's Doctor Who's own tardis, yes.  We 'liberated'
    ty:it just for you!
    done
  n:crown,headband,tiara,jewelry
    if IN$ tiara
      ty:It's actually a cheap plastic one with glass
      ty:jewels.  Looks good though, don't it!
      done
    else
      ty:You don't have that!
      done
    endif
  n:gun,raygun
    if IN$ gun
      ty:It looks pretty powerful. Too bad it's empty.
      done
    else
      ty:You don't have that!
      done
    endif
  nend
  n:disk
    if IN$ disk
      ty:It's a standard 3 1/2 inch disk. Of course, you
      ty:can't read it directly.
      done
    else
      ty:You don't have that!
      done
    endif
  n:computer
    if room=1
      ty:It's an Amiga, all right. Of course, you
      ty:didn't think we would use anything else,
      ty:did you?
      done
    else
      ty:You don't see that!
      done
    endif
  nend
  ty:examine what!?!
  done

v:put,insert,boot
  n:disk
    p:drive,diskdrive,computer,amiga
     if IN$ disk
      if room=1
        ty:The only file on this disk is the
        ty:following shameless plug:
        ty:
        ty:   SunRize Industries is the producer of
        ty:the Perfect Sound audio digitizer, a low-cost,
        ty:8-bit, stereo digitizer for the Amiga.
        ty:   We have also created City Desk, an extremely
        ty:powerful desktop publishing program.
        ty:   Besides the adventures you will soon see that
        ty:we have created on the Adventure-Pro system, we
        ty:are marketing Studio Magic, a versatile
        ty:sound editing program that... that.. well, you
        ty:just have to see it.  Also, we will soon be
        ty:marketing a real-time video digitizer.
        ty:
        ty:Your local dealers should have these superb products.
        ty:If they don't, ask them why, and then feel free
        ty:to call us.  Working hours are 9-6, and our phone is
        ty:(409)-846-1311 .
        ty:
        done
      else
        ty:There's no computer here!
      endif
      done
     else
      ty:You don't have a disk!
     endif
     done
    poend
    ty:Insert it where!?!
    done

BACKGROUND

if room>1
  if pourf=1
    unslave pour
    clearsound pour
    pourf=0
  endif
endif

if room=3
else
  and
  if beepf=1
    beepf=0
    stopsound trek3
    clearsound trek3
endif
//
if room=2
else
  and
  if windf=1
    windf=0
    stopsound wind
    clearsound wind
endif

if tardisf=0
  tardisf=1
  loadsound tard3
endif  //

if room=1
  if pourf=0
    loadsound pour
    slave pour 2 4 64
    pourf=1
  endif
endif

if room=3
  and
  if beepf=0
    beepf=1
    loadsound trek3
    playsound trek3 0 45
endif

//if room=2
  and
  if windf=0
    windf=1
    loadsound wind
    playsound wind 0 45
endif  //




