100 CLS:CLEAR 1000
110 DEFSTR R,V,H
120 PC$="PHRASE TO COMPLEX."
130 DIM R(50),I(10),P(27,10),N(27),S(27),E(27),W(27),RD$(27)
140 PRINT"SMUGGLER'S VOYAGE  (C) 1982 ANTHONY WOOD"
150 PRINT"DISTRIBUTED BY CYBERNETIC DESIGN"
160 FOR C=1 TO 27:READ N(C),S(C),E(C),W(C):NEXT
170 READ RD(1):FOR U=2 TO 8:RD(U)=RD(U-1):NEXT
180 READ RD(9),RD(10):RD(11)=RD(10):RD(12)=RD(10)
190 FOR C=13 TO 27:READ RD(C):NEXT:K=0
200 READ R(K):IF R(K)="*" THEN K=K-1 :ELSE K=K+1:GOTO 200
210 P(8,0)=1:P(8,1)=1
220 P(13,0)=1:P(13,1)=3
230 P(14,0)=1:P(14,1)=21
240 P(15,0)=2:P(15,1)=4:P(15,2)=22
250 P(16,0)=2:P(16,1)=23:P(16,2)=24
260 P(17,0)=2:P(17,1)=25:P(17,2)=26
270 P(18,0)=1:P(18,1)=8
280 P(11,0)=1:P(11,1)=2
290 P(20,0)=1:P(20,1)=23:P(21,0)=2:P(21,1)=12:P(21,2)=25
300 P(23,0)=1:P(23,1)=13
310 CLS:L=13:CP=448
320 FOR Y=0 TO 5:PRINT@Y*64,STRING$(63," "):NEXT:PRINT@0,"I'M ";RD(L)
330 IF N(L)=0 AND S(L)=0 AND E(L)=0 AND W(L)=0 THEN 350
340 PRINT"OBVIOUS EXITS: ";
350 IF N(L)<>0 THEN PRINT"NORTH. ";
360 IF S(L)<>0 THEN PRINT"SOUTH. ";
370 IF E(L)<>0 THEN PRINT"EAST. ";
380 IF W(L)<>0 THEN PRINT"WEST.";
390 PRINT:IF P(L,0)=0 THEN 410
400 PRINT"I SEE: ";:FOR Y=1 TO P(L,0):PRINTR(P(L,Y));". ";:NEXT
410 PRINT@384,"<";STRING$(59,"-");">";
420 PRINT@CP,"";:LINEINPUT"> ";H:CP=CP+64
430 IF CP>895 THEN CP=448
440 IF H="N" OR H="NORTH" THEN T=N(L):GOTO 490
450 IF H="S" OR H="SOUTH" THEN T=S(L):GOTO 490
460 IF H="E" OR H="EAST" THEN T=E(L):GOTO 490
470 IF H="W" OR H="EAST" THEN T=W(L):GOTO 490
480 GOTO 510
490 IF T=0 THEN PRINT@CP,"I CAN'T GO IN THAT DIRECTION.":CP=CP+64:GOTO 420
500 L=T:GOTO 320
510 X=0
520 Z=INSTR(H," "):IF Z=0 THEN W$(X)=H:GOTO 540
530 GOSUB 9090:H=RIGHT$(H,LEN(H)-Z):GOTO 520
540 FOR U=0 TO X:B(U)=-1:NEXT:FOR Y=0 TO X:FOR U=0 TO K:IF INSTR(R(U),W$(Y))<>0 THEN B(Y)=U
550 NEXT:NEXT
560 V$=W$(0):IF V$="GET" OR V$="TAKE" OR V$="PICK" THEN 570 :ELSE 670
570 IF X<>1 THEN PRINT @ CP,PC$:CP=CP+64:GOTO 420
580 IF B(1)=-1 THEN PRINT@CP,"I DON'T KNOW WHAT A ";W$(1);" IS.":CP=CP+64:GOTO 420
590 IF B(1)=1 OR B(1)=2 OR B(1)=3 ORB(1)=9 OR B(1)=13 OR B(1)=14 OR B(1)=16 OR B(1)=17 OR B(1)=19 OR (B(1)<19 AND B(1)>28) THEN CP=CP+64:PRINT@CP,"DON'T BE STUPID.":CP=CP+64:GOTO 320
600 Q=B(1):GOSUB 9010:GOTO 320
670 IF W$(0)<>"INVENTORY" THEN 700
680 IF IC=0 THEN PRINT"YOU ARN'T CARRYING ANYTHING.":CP=CP+64:GOTO 320
690 FOR Y=1 TO IC:PRINTR(I(Y)):CP=CP+64:NEXT:GOTO 420
700 IF W$(0)<>"SIT" THEN 750
710 IF W$(1)="DOWN" OR W$(1)="" THEN 730
720 IF W$(1)="UP" THEN 740 :ELSE PRINT@CP,"PARDON?":CP=CP+64:GOTO 420
730 IF L=14 THEN L=17::PRINT@CP,"OK":CP=CP+64:GOTO 320 :ELSE PRINT@CP,"I DON'T LIKE TO SIT ON THE FLOOR.":CP=CP+64:GOTO 420
740 IF L=17 THEN L=14:CP=CP+64:PRINT@CP,"OK":GOTO 320 :ELSE CP=CP+64:PRINT@CP,"HUH":GOTO 420
750 IF W$(0)="PRESS" OR W$(0)="PUSH" THEN 760 :ELSE850
760 IF W$(1)="BUTTON" AND L<>14 THEN PRINT@CP,"WHICH BUTTON?":CP=CP+64:GOTO 420
770 IF W$(1)<>"BUTTON" AND W$(2)<>"BUTTON"THEN PRINT"PARDON?":CP=CP+64:GOTO 420
780 IF L=17 THEN 790
782 IF L=16 THEN800:ELSE PRINT@CP,"THERE'S KNOW BUTON HERE.":CP=CP+64:GOTO 420
790 IF BP=0 THEN PRINT@CP,"THE VIDEO SCREEN TURNS ON, AND DISPLAYS:1) TAKE OFF
2) SCREENS":CP=CP+128:GOTO 420 :ELSE PRINT@CP,"NOTHING HAPPENS.":CP=CP+64:GOTO 420
800 IF W$(1)="BROWN" THEN 830
810 IF W$(1)="YELLOW" THEN 820 :ELSE PRINT@CP,"WHAT??":CP=CP+64:GOTO 420
820 IF AC=1 THEN PRINT@CP,"A DOOR SLIDES OPEN TO REVEIL THE LUNAR SURFACE":CP=CP+64:S(16)=8:GOTO 320 :ELSE PRINT@CP,"NOTHING HAPPENS":CP=CP+64:GOTO 420
830 IF AC=0 THEN PRINT@CP,"THE AIR CYCLES OUT OF THE ROOM.":AC=1 :ELSE PRINT@CP,"THE AIR REFILLS THE ROOM.":AC=0
840 CP=CP+64:IF AC=1 THEN A$="SPACE SUIT":GOSUB 9040:IF F=0 THEN PRINT@CP,"AGGHH... I CAN'T BREATH.":END :ELSE 420
850 IF W$(0)="OPEN" THEN 860 :ELSE 890
860 IF W$(1)<>"CLOSET" THEN PRINT@CP,"HUH":CP=CP+64:GOTO 420
870 IF CL=0 THEN CL=1:PRINT@CP,"I FOUND A SPACE SUIT HERE.":CP=CP+64:A=0:GOSUB 9080:GOTO 320
880 PRINT@CP,"I'TS EMPTY.":CP=CP+64:GOTO 320
890 IF W$(0)="TYPE" THEN 900 :ELSE
900 IF L<>17 THEN PRINT@CP,"THERE'S NOTHING TO TYPE ON HERE.":CP=CP+64:GOTO 420
910 IF W$(1)="ONE" OR W$(1)="1" THEN 920 :ELSE 950
920 IF NP=0 THEN PRINT@CP,"THE SHIP STARTS TO WHINE...
ON THE SCREEN COMES THE MSG 'EMERGENCY ENGINE SHUT DOWN'":CP=CP+128:GOTO 420
930 PRINT@CP,"THE SHIP HUMS SOFTLY... ON THE SCREEN IS DIPLAYED 
'ARIVAL IN 30 TURNS'":CP=CP+128:GOTO 420
950 IF W$(1)="TWO" OR W$(1)="2" THEN PRINT@CP,"THE SCREEN SAYS 'DONE'":CP=CP+64:SH=1:GOTO 420
960 PRINT@CP,"HUH?":GOTO 420
9000 REM ADD ITEM "Q" TO INVENTORY
9010 FOR Z=1 TO P(L,0):IF P(L,Z)=Q THEN 9020 :ELSE NEXT:PRINT@CP,"THERE ISN'T A";W$(1);" HERE.":CP=CP+64:RETURN
9020 P(L,0)=P(L,0)-1:FOR ZZ=Z TO P(L,0):P(L,ZZ)=P(L,ZZ+1):NEXT:PRINT@CP,"OK":CP=CP+64:IC=IC+1:I(IC)=B(1):RETURN
9030 REM SEE IF A$ IS IN THE INVENTORY
9040 IF IC=0 THEN F=0:RETURN :ELSEFOR Z=1 TO IC:IF R(I(Z))=A$ THEN F=1:RETURN :ELSE NEXT:F=0:RETURN
9050 REM SEE IF A$ IS IN ROOM
9060 IF P(L,0)=0 THEN F=0:RETURN
9070 FOR Z=1 TO P(L,0):IF R(P(L,Y))=A$ THEN F=1:RETURN :ELSE NEXT:F=0:RETURN
9080 P(L,0)=P(L,0)+1:P(L,P(L,0))=A:RETURN :REM'ADD TO ROOM
9090 A$=LEFT$(H,Z-1):IF A$="THE" OR A$="A" OR A$="AN" THEN RETURN
9100 W$(X)=A$:X=X+1:RETURN
9110 DATA 12,2,9,0	
9120 DATA 1,3,7,0, 2,0,8,0, 5,0,0,8, 6,4,0,7, 10,5,0,9
9130 DATA 9,8,5,2, 7,0,4,3, 11,7,6,1, 0,6,0,11, 0,9,10,12
9140 DATA 0,1,11,0, 0,16,14,15, 0,0,0,13, 0,0,13,0, 13,8,0,0
9150 DATA 0,0,0,0, 0,0,0,0
9160 DATA 0,0,0,0, 0,0,0,0, 0,0,0,20, 0,0,21,19, 0,23,22,20
9170 DATA 0,0,0,21, 21,24,0,0, 23,0,0,0, 0,0,0,0
9180 DATA"ON A BARREN LUNER PLAIN."
9190 DATA"ON A BARREN LUNER PLAIN. FAR TO THE NORTH I CAN SEE
AN AUTOMATED SCOUTING SHIP."
9200 DATA"ON A BARREN LUNER PLAIN. AN IMPASSABLE MOUNTAIN
CHAIN LIES TO THE NORTH."
9210 DATA"IN THE LIVING SECTION OF A SMALL SPACE SHIP."
9220 DATA"IN THE PILOT'S CABIN OF A SMALL SPACE SHIP.
THE EASTERN WALL IS COVERED BY A CONTROL CONUCEL."
9230 DATA"IN THE ENGINE ROOM OF A SMALL SPACE SHIP."
9240 DATA"IN AN AIRLOCK."
9250 DATA"SITTING IN A SEAT IN FRON OF A CONTROL COUNCEL."
9260 DATA"IN THE SERVICE COMPARTMENT OF AN AUTOMATED SCOUTING SHIP."
9270 DATA"ON AN ALIEN LANDING PAD."
9280 DATA"IN AN ELEVATOR."
9290 DATA"IN A DAMP ROOM. A SHALOW POOL LIES IN BETWEEN
ME AN THE DOOR ON THE EAST WALL."
9300 DATA"ON THE EAST SIDE OF A SHALLOW POOL."
9310 DATA"IN A LONG DESERTED ARCADE. RUBBLE IS SCATTERED ALONG THE
WALLS IN WHAT IS OBVIOUSLY THE REMAINS OF VIDIO GAMES. ON GAME IS STILL IN ONE PIECE, BUT NOT IN ANY CONDITION TO PLAY."
9320 DATA"IN A SNACK AREA. BROKEN TABLES LITER THE FLOOR ALONG
WITH A FEW BROKEN VENDING MACHINES. ON COKE MACHINE LOOKS AS IF
IT COULD WORK."
9330 DATA"IN A SMALL ROOM THAT GIVES NO INDICATION OF ITS PURPOSE."
9340 DATA"IN A LARGE CAVERN THAT COULD HAVE BEEN A PARK. WEEDS HAVE
OVERGROWN EVERYTHING NOW, INCLUDING A CRUMBLING WELL."
9350 DATA"I'M IN A DANK PIT."
9360 DATA "SPACE SUIT","SMALL SPACE SHIP","AUTOMATED SCOUTING SHIP","CLOSET","TOOL BOX","SCREWDRIVER","ACCESS PANEL","BURNT OUT FIELD GENERATOR","NEW FIELD GENERATOR","ELEVATOR","PARANA","LIGHT BULB","SWITCH","ROBOT"
9370 DATA "COIN RETURN","COKE CAN","COKE MACHINE","POOL OF ACID","ROPE","OLD WELL","GARDIAN","CHAIR","INTER-CO ENGINE","BROWN BUTTON","YELLOW BUTTON","BLACK BUTTON","CALCULATOR KEYBORD","SHALLOW POOL"
9380 DATA "*"
